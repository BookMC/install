plugins {
    id "java"
    id "maven-publish"
    id "org.bookmc.buildtools" version "0.3.3+build.15"
}

ext {
    def env = System.getenv()
    buildNumber = env.BUILD_NUMBER
    local = buildNumber == null
    branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group "org.bookmc"
version "0.1.2${branch != "main" ? "-$branch" : ""}+${buildNumber ? "build.$buildNumber" : "local"}"

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.7"

    implementation group: "org.apache.logging.log4j", name: "log4j-api", version: "2.14.1"
    implementation group: "org.apache.logging.log4j", name: "log4j-core", version: "2.14.1"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"

    implementation group: "commons-io", name: "commons-io", version: "2.11.0"
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "book-loader"
                url = "https://github.com/BookMC/loader"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}