plugins {
    id "java"
    id "maven-publish"
    id "org.bookmc.buildtools" version "0.3.8+build.29"
}

ext {
    branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group "org.bookmc"
version "0.3.1${branch != "main" && !branch.isEmpty() ? "-$branch" : ""}"

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.7"

    implementation "org.apache.logging.log4j:log4j-api:2.14.1"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"

    implementation "commons-io:commons-io:2.11.0"
}

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean login = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (login) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = !login ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "BookMC"
                    }
                }
            }
        }
    }
}